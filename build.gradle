buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.1.8"
    id 'net.researchgate.release' version '2.4.0'
}


apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'idea'

sourceCompatibility = 1.8

idea {
    module {
        generatedSourceDirs += file('gen')
    }
}

sourceSets {
    main {
        java.srcDirs += 'gen'
    }
}

clean.doFirst {
    delete 'gen'
}

intellij {
    pluginName 'Intellij-Solidity'

    version '2016.3'
    downloadSources Boolean.valueOf(true)
    updateSinceUntilBuild = false

    sandboxDirectory project.rootDir.canonicalPath + "/.sandbox"
    systemProperties = ['compiler.process.debug.port': '21232']
}

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

release {
    newVersionCommitMessage = '[Intellij-Solidity Release] - '
    preTagCommitMessage = '[Intellij-Solidity Release] - pre tag commit: '
    buildTasks = ['buildPlugin']
}


task codegen(dependsOn: ['lexer', 'parser']) {}

task lexer(type: JavaExec) {
    def lexerName = "_SolidityLexer"
    def pkg = "me/serce/solidity"
    def src = "$project.projectDir/src/main/grammars/${lexerName}.flex"
    def dst = "gen/$pkg"
    new File(dst).mkdirs()

    main = 'jflex.Main'
    classpath = files('lib/jflex-1.7.0-SNAPSHOT.jar')

    args = ['--skel', 'src/main/grammars/idea-flex.skeleton', '-d', dst, src]

    inputs.file file(src)
    outputs.dir file("$dst/_${lexerName}.java")
}

task parser(type: JavaExec) {
    def pkg = 'me/serce/solidity/lang'
    def dstRoot = "gen"
    def src = "$project.projectDir/src/main/grammars/solidity.bnf"
    def dst = "$dstRoot/$pkg"
    doFirst {
        delete file(dst)
    }

    main = 'org.intellij.grammar.Main'
    classpath(configurations.compile + files('lib/grammar-kit-1.5.0.jar'))

    args = [dstRoot, file(src)]

    inputs.file file(src)
    outputs.dir dst
}
